port := env_var_or_default('PORT', '3080')

# Tailwind in watch mode
tailwind:
	npx tailwindcss -i ./styles/input.css -o ./styles/output.css --watch

# svg sprite creation
# TODO change route on svetle project deletion 
# TODO #directory-swap
svgstore:
   npx svgstore -o ../static/svg/icons-sprite.svg ../src/svg/**.svg

server_dev: 
	cargo watch -x run

# CMS server for local dev
# TODO #directory-swap
decap_server:
	cd .. && npx decap-server

# Run dev server in watch mode
dev: 
	(just server_dev; just tailwind) | parallel

# Run dev server in watch mode
test: 
	cargo test

# Run server in production mode
prod:
    cargo run --release

# Wait for port to listen to connections
wait_for_port:
	#!/usr/bin/env bash
	set -euxo pipefail
	while ! nc -z localhost {{port}}; do
	sleep 3
	done

# Kill the application running on port
kill:
	kill $(lsof -t -i:{{port}})

# Clean the dist folder
clean:
	rm -rf dist

# SSG export of production server
export: clean
	just prod &
	just wait_for_port
	- wget --convert-links -r -p --level 1 -E -P dist --no-host-directories localhost:{{port}}
	just kill
